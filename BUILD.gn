# Copyright (C) 2023 Grexie. Portions copyright
# 2014 the Chromium Authors and 2016 the CEF Authors. All rights reserved. Use of this source code is
# governed by a BSD-style license that can be found in the LICENSE file.
#
# This file provides the GN configuration for the Honeycomb project. This is not a
# stand-alone configuration -- it must be built from inside the Chromium source
# tree. See https://bitbucket.org/chromiumembedded/honey/wiki/BranchesAndBuilding
# for complete Honeycomb build instructions. See below for links to additional GN
# documentation.
#
# GN Setup:
#
#    Optionally configure GN by setting the `GN_DEFINES` and/or `GN_ARGUMENTS`
#    environment variables.
#
#    Example A: Create an official build on Windows:
#
#      > set GN_DEFINES=is_official_build=true
#
#    Example B: Generate VS2017 project files in addition to the default Ninja
#               build files on Windows:
#
#      > set GN_ARGUMENTS=--ide=vs2017 --sln=honey --filters=//honey/*
#
#      After completing the "GN Automated Build" or "GN Manual Build" section
#      open "out\<build_dir>\honey.sln" for editing and debugging. Building must
#      still be performed using the Ninja command-line.
#
# GN Automated Build:
#
#    Run the `automate-git.py` script as described on the BranchesAndBuilding
#    Wiki page. GN, unlike GYP, supports parallel build configurations. The
#    following command-line flags have special meaning with GN builds:
#
#    --x64-build    Perform an x64 build if specified (requires out/*_GN_x64
#                   directories), otherwise perform an x86 build (requires
#                   out/*_GN_x86 directories).
#
#    Directories are created subject to your platform and `GN_DEFINES` settings.
#    See Step 1B in the "GN Manual Build" section below for details.
#
# GN Manual Build:
#
# 1. Run the `honey_create_projects.[bin|sh]` script. Upon successful completion
#    proceed to Step 2.
#
#    The `honey_create_projects.[bin|sh]` script will automatically do all of the
#    following:
#
#    A. Apply patch files to the Chromium source tree. This includes
#       `patch/patches/gn_config.patch` which is required for GN integration.
#
#    B. Create multiple build output directories appropriate to your platform
#       and `GN_DEFINES` settings. For example:
#
#         x86 debug build   -> out/Debug_GN_x86
#         x86 release build -> out/Release_GN_x86
#         x64 debug build   -> out/Debug_GN_x64
#         x64 release build -> out/Release_GN_x64
#
#       Build output directories will be created subject to the following rules
#       (defined in `tools/gn_args.py` GetAllPlatformConfigs):
#
#       - Debug and Release directories will be created on all platforms by
#         default. Debug directories will not be created when `is_asan=true`.
#       - x64 directories will always be created on all platforms.
#       - x86 directories will always be created on Windows.
#       - x86 directories will be created on Linux when `use_sysroot=true`.
#
#    C. Write the `args.gn` file for each build output directory using the
#       `tools/gn_args.py` script to determine the GN arguments. Some arguments
#       are required and some are optional. See script output for details in
#       case of conflicts or recommendations.
#
#    D. Run `gn gen` for each build output directory to generate project files.
#       Ninja files will be generated by default. Additional command-line
#       arguments (including other project formats) can be specified via the
#       `GN_ARGUMENTS` environment variable. Run `gn gen --help` for a list of
#       supported arguments.
#
# 2. Run Ninja from the command-line to build. If the build configuration has
#    changed it will automatically re-run `gn gen` with the same arguments.
#
#    > ninja -C out/<build_dir> honeyclient honeysimple honeytests
#
# GN Manual Packaging:
#
#    Run the `make_distrib.[bat|sh]` script as described on the
#    BranchesAndBuilding Wiki page.
#
# Additional GN documentation:
# http://www.chromium.org/developers/gn-build-configuration
# https://chromium.googlesource.com/chromium/src/+/master/tools/gn/docs/language.md
#

import("//base/allocator/allocator.gni")
import("//build/config/features.gni")
import("//build/config/locales.gni")
import("//build/config/ozone.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/ui.gni")
import("//chrome/common/features.gni")
import("//content/public/app/mac_helpers.gni")
import("//extensions/buildflags/buildflags.gni")
import("//media/media_options.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//ppapi/buildflags/buildflags.gni")
import("//printing/buildflags/buildflags.gni")
import("//rlz/buildflags/buildflags.gni")
import("//testing/test.gni")
import("//third_party/icu/config.gni")
import("//third_party/widevine/cdm/widevine.gni")
import("//tools/grit/repack.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")
import("//ui/gl/features.gni")
import("//v8/gni/v8.gni")
if (is_clang) {
  import("//build/config/clang/clang.gni")
}
if (is_linux) {
  import("//build/config/linux/pkg_config.gni")
  import("//third_party/fontconfig/fontconfig.gni")
}
if (is_mac) {
  import("//build/apple/tweak_info_plist.gni")
  import("//build/config/mac/rules.gni")
  import("//chrome/version.gni")
  import("//media/cdm/library_cdm/cdm_paths.gni")

  # Template to compile .xib and .storyboard files.
  #
  # Arguments
  #
  #     sources:
  #         list of string, sources to compile
  #
  #     ibtool_flags:
  #         (optional) list of string, additional flags to pass to the ibtool
  template("compile_ib_files") {
    action_foreach(target_name) {
      forward_variables_from(invoker,
                            [
                              "testonly",
                              "visibility",
                            ])
      assert(defined(invoker.sources),
            "sources must be specified for $target_name")
      assert(defined(invoker.output_extension),
            "output_extension must be specified for $target_name")

      ibtool_flags = []
      if (defined(invoker.ibtool_flags)) {
        ibtool_flags = invoker.ibtool_flags
      }

      _output_extension = invoker.output_extension

      script = "//honey/tools/compile_ib_files.py"
      sources = invoker.sources
      outputs = [
        "$target_gen_dir/$target_name/{{source_name_part}}.$_output_extension",
      ]
      args = [
        "--input",
        "{{source}}",
        "--output",
        rebase_path(
            "$target_gen_dir/$target_name/{{source_name_part}}.$_output_extension",
            root_build_dir),
      ]
      if (!use_system_xcode) {
        args += [
          "--developer_dir",
          hermetic_xcode_path,
        ]
      }
      args += ibtool_flags
    }
  }

  # Template to compile and package Mac XIB files as bundle data.
  #
  # Arguments
  #
  #     sources:
  #         list of string, sources to comiple
  #
  #     output_path:
  #         (optional) string, the path to use for the outputs list in the
  #         bundle_data step. If unspecified, defaults to bundle_resources_dir.
  template("mac_xib_bundle_data") {
    _target_name = target_name
    _compile_target_name = _target_name + "_compile_ibtool"

    compile_ib_files(_compile_target_name) {
      forward_variables_from(invoker, [ "testonly" ])
      visibility = [ ":$_target_name" ]
      sources = invoker.sources
      output_extension = "nib"
      ibtool_flags = [
        "--minimum-deployment-target",
        mac_deployment_target,

        # TODO(rsesek): Enable this once all the bots are on Xcode 7+.
        # "--target-device",
        # "mac",
      ]
    }

    bundle_data(_target_name) {
      forward_variables_from(invoker,
                            [
                              "testonly",
                              "visibility",
                            ])

      public_deps = [
        ":$_compile_target_name",
      ]
      sources = get_target_outputs(":$_compile_target_name")

      _output_path = "{{bundle_resources_dir}}"
      if (defined(invoker.output_path)) {
        _output_path = invoker.output_path
      }

      outputs = [
        "$_output_path/{{source_file_part}}",
      ]
    }
  }
}
if (is_win) {
  import("//build/config/win/console_app.gni")
  import("//build/config/win/manifest.gni")
}

if (is_linux) {
  declare_args() {
    # The honeyclient target depends on GTK packages that are not available in the
    # default sysroot environment. So we should only use them when we are not
    # using the sysroot. Alternatively, the developer might not want to use the
    # GTK dependencies at all, in which case they can set `honey_use_gtk=false`.
    honey_use_gtk = !use_sysroot
  }
}

#
# Verify required global arguments configured via `gn args`.
# Set by GetRequiredArgs() in //honey/tools/gn_args.py.
#

# Set ENABLE_PRINTING=1 ENABLE_BASIC_PRINTING=1.
assert(enable_basic_printing)
assert(enable_print_preview)

# Enable support for Widevine CDM.
assert(enable_widevine)

if (is_mac || is_win) {
  # Enable Widevine CDM host verification and storage ID.
  assert(enable_cdm_host_verification)
  assert(enable_cdm_storage_id)
  assert(alternate_cdm_storage_id_key != "")
  assert(enable_rlz)
}

# Enable Views UI framework.
assert(toolkit_views)

if (is_clang) {
  # Don't use the chrome style plugin.
  assert(!clang_use_chrome_plugins)
}


#
# Local variables.
#

if (is_mac) {
  # The tweak_info_plist.py script requires a version number with 4 parts. Honeycomb
  # uses a version number with 3 parts so just set the last part to 0.
  honey_plist_version = exec_script(
    "//honey/tools/honey_version.py", [ "plist" ], "trim string", [])

  # Need to be creative to match dylib version formatting requirements.
  honey_dylib_version = exec_script(
    "//honey/tools/honey_version.py", [ "dylib" ], "trim string", [])
}

# Read file lists from gypi files. The gypi_to_gn.py script does not support
# variable references so all required variables must be explicitly specified in
# the below configurations.
gypi_paths = exec_script("//honey/tools/gypi_to_gn.py",
                         [ rebase_path("honey_paths.gypi") ],
                         "scope",
                         [ "honey_paths.gypi" ])
gypi_paths2 = exec_script("//honey/tools/gypi_to_gn.py",
                          [ rebase_path("honey_paths2.gypi") ],
                          "scope",
                          [ "honey_paths2.gypi" ])

includes_common = gypi_paths2.includes_common + gypi_paths2.includes_common_capi
includes_mac = gypi_paths2.includes_mac + gypi_paths2.includes_mac_capi
includes_linux = gypi_paths2.includes_linux + gypi_paths2.includes_linux_capi
includes_win = gypi_paths2.includes_win + gypi_paths2.includes_win_capi

#
# Targets that will be built when depending on "//honey".
#

group("honey") {
  testonly = true
  deps = [
    ":honeysimple",
    ":honeytests",
    ":libhoneycomb_static_unittests",
  ]

  if (!is_linux || ozone_platform_x11) {
    deps += [ ":honeyclient" ]
  }
}


if (is_win) {
  # Target for building code that accesses chrome_elf internals. Included from
  # the //chrome_elf:crash target. Defined as a static_library instead of a
  # source_set because (a) the source files don't export any symbols and (b)
  # *_switches.cc duplication otherwise causes linker errors.
  static_library("chrome_elf_set") {
    sources = [
      "libhoneycomb/common/crash_reporter_client.cc",
      "libhoneycomb/common/crash_reporter_client.h",

      # Required for crash_keys::GetChromeCrashKeys.
      # Otherwise we need to copy this array into Honeycomb, which would be difficult
      # to maintain.
      "//chrome/common/crash_keys.cc",
      "//chrome/common/chrome_switches.cc",
      "//components/flags_ui/flags_ui_switches.cc",
      "//content/public/common/content_switches.cc",
    ]

    configs += [
      "libhoneycomb/features:config",
      "//build/config:precompiled_headers",
    ]

    if (is_component_build) {
      # Avoid linker errors with content_switches.cc in component build by not
      # defining CONTENT_EXPORT.
      defines = ["COMPILE_CONTENT_STATICALLY"]
    }

    deps = [
      "//components/crash/core/common",  # crash_keys

      # Required by chrome_switches.cc
      "//chrome/common:buildflags",
      "//ppapi/buildflags:buildflags",
      "//printing/buildflags:buildflags",
      "//ui/base:buildflags",

      # Required by content_switches.cc
      "//media:media_buildflags",

      # Required by crash_keys.cc (from base/stl_util.h)
      "//third_party/abseil-cpp:absl",
    ]
  }
}


#
# Test support targets.
#

# Source files that are linked into libhoneycomb and honey_framework and tested by
# libhoneycomb_static_unittests. These sources provide libhoneycomb-internal functionality
# that is limited in scope (e.g. utility classes/methods).
source_set("libhoneycomb_static_unittested") {
  sources = [
    "libhoneycomb/browser/devtools/devtools_util.cc",
    "libhoneycomb/browser/devtools/devtools_util.h",
    "libhoneycomb/browser/geometry_util.h",
    "libhoneycomb/browser/geometry_util.cc",
  ]

  deps = [
    "//base",
    "//ui/gfx/geometry",
  ]

  configs += [
    "libhoneycomb/features:config",
    "//build/config:precompiled_headers",
  ]
}

# Executable target that provides test coverage for libhoneycomb_static_unittested
# source files.
test("libhoneycomb_static_unittests") {
  testonly = true

  sources = [
    "libhoneycomb/browser/devtools/devtools_util_unittest.cc",
    "libhoneycomb/browser/geometry_util_unittest.cc",
  ]

  deps = [
    ":libhoneycomb_static_unittested",
    "//base/test:run_all_unittests",
    "//testing/gtest",
  ]

  configs += [
    "libhoneycomb/features:config",
    "//build/config:precompiled_headers",
  ]
}

# Source files that are linked into libhoneycomb and honey_framework and implement
# interfaces exposed to honeytests via the include/test directory. These sources
# may access Chromium/Honeycomb internals. This is defined as a separate target from
# libhoneycomb_static to avoid introducing testonly dependencies there.
source_set("libhoneycomb_test_support") {
  testonly = true

  sources = [
    "libhoneycomb/browser/test/test_helpers_impl.cc",
    "libhoneycomb/browser/test/test_server_impl.cc",
    "libhoneycomb/browser/test/test_server_impl.h",
    "libhoneycomb/common/test/translator_test_impl.cc",
  ]

  deps = [
    ":libhoneycomb_static",
    "//net:test_support",

    # Support for UI input events.
    "//ui/views:test_support",
  ]
}


#
# libhoneycomb_static target.
#

source_set("libhoneycomb_static") {
  sources = includes_common +
            gypi_paths.autogen_cpp_includes + [
    "libhoneycomb/browser/alloy/alloy_browser_context.cc",
    "libhoneycomb/browser/alloy/alloy_browser_context.h",
    "libhoneycomb/browser/alloy/alloy_browser_host_impl.cc",
    "libhoneycomb/browser/alloy/alloy_browser_host_impl.h",
    "libhoneycomb/browser/alloy/alloy_browser_main.cc",
    "libhoneycomb/browser/alloy/alloy_browser_main.h",
    "libhoneycomb/browser/alloy/alloy_content_browser_client.cc",
    "libhoneycomb/browser/alloy/alloy_content_browser_client.h",
    "libhoneycomb/browser/alloy/alloy_download_util.cc",
    "libhoneycomb/browser/alloy/alloy_download_util.h",
    "libhoneycomb/browser/alloy/alloy_web_contents_view_delegate.cc",
    "libhoneycomb/browser/alloy/alloy_web_contents_view_delegate.h",
    "libhoneycomb/browser/alloy/browser_platform_delegate_alloy.cc",
    "libhoneycomb/browser/alloy/browser_platform_delegate_alloy.h",
    "libhoneycomb/browser/alloy/dialogs/alloy_constrained_window_views_client.cc",
    "libhoneycomb/browser/alloy/dialogs/alloy_constrained_window_views_client.h",
    "libhoneycomb/browser/alloy/dialogs/alloy_javascript_dialog_manager_delegate.cc",
    "libhoneycomb/browser/alloy/dialogs/alloy_javascript_dialog_manager_delegate.h",
    "libhoneycomb/browser/alloy/dialogs/alloy_web_contents_dialog_helper.cc",
    "libhoneycomb/browser/alloy/dialogs/alloy_web_contents_dialog_helper.h",
    "libhoneycomb/browser/alloy/chrome_browser_process_alloy.cc",
    "libhoneycomb/browser/alloy/chrome_browser_process_alloy.h",
    "libhoneycomb/browser/alloy/chrome_profile_manager_alloy.cc",
    "libhoneycomb/browser/alloy/chrome_profile_manager_alloy.h",
    "libhoneycomb/browser/alloy/chrome_profile_alloy.cc",
    "libhoneycomb/browser/alloy/chrome_profile_alloy.h",
    "libhoneycomb/browser/audio_capturer.cc",
    "libhoneycomb/browser/audio_capturer.h",
    "libhoneycomb/browser/audio_loopback_stream_creator.cc",
    "libhoneycomb/browser/audio_loopback_stream_creator.h",
    "libhoneycomb/browser/browser_contents_delegate.cc",
    "libhoneycomb/browser/browser_contents_delegate.h",
    "libhoneycomb/browser/browser_context.cc",
    "libhoneycomb/browser/browser_context.h",
    "libhoneycomb/browser/browser_context_keyed_service_factories.cc",
    "libhoneycomb/browser/browser_context_keyed_service_factories.h",
    "libhoneycomb/browser/browser_frame.cc",
    "libhoneycomb/browser/browser_frame.h",
    "libhoneycomb/browser/browser_host_base.cc",
    "libhoneycomb/browser/browser_host_base.h",
    "libhoneycomb/browser/browser_host_create.cc",
    "libhoneycomb/browser/browser_info.cc",
    "libhoneycomb/browser/browser_info.h",
    "libhoneycomb/browser/browser_info_manager.cc",
    "libhoneycomb/browser/browser_info_manager.h",
    "libhoneycomb/browser/browser_manager.cc",
    "libhoneycomb/browser/browser_manager.h",
    "libhoneycomb/browser/browser_message_loop.cc",
    "libhoneycomb/browser/browser_message_loop.h",
    "libhoneycomb/browser/browser_platform_delegate.cc",
    "libhoneycomb/browser/browser_platform_delegate.h",
    "libhoneycomb/browser/browser_platform_delegate_create.cc",
    "libhoneycomb/browser/browser_util.cc",
    "libhoneycomb/browser/browser_util.h",
    "libhoneycomb/browser/certificate_query.cc",
    "libhoneycomb/browser/certificate_query.h",
    "libhoneycomb/browser/chrome/browser_delegate.h",
    "libhoneycomb/browser/chrome/browser_platform_delegate_chrome.cc",
    "libhoneycomb/browser/chrome/browser_platform_delegate_chrome.h",
    "libhoneycomb/browser/chrome/chrome_browser_context.cc",
    "libhoneycomb/browser/chrome/chrome_browser_context.h",
    "libhoneycomb/browser/chrome/chrome_browser_delegate.cc",
    "libhoneycomb/browser/chrome/chrome_browser_delegate.h",
    "libhoneycomb/browser/chrome/chrome_browser_host_impl.cc",
    "libhoneycomb/browser/chrome/chrome_browser_host_impl.h",
    "libhoneycomb/browser/chrome/chrome_browser_main_extra_parts_honey.cc",
    "libhoneycomb/browser/chrome/chrome_browser_main_extra_parts_honey.h",
    "libhoneycomb/browser/chrome/chrome_content_browser_client_honey.cc",
    "libhoneycomb/browser/chrome/chrome_content_browser_client_honey.h",
    "libhoneycomb/browser/chrome/chrome_context_menu_handler.cc",
    "libhoneycomb/browser/chrome/chrome_context_menu_handler.h",
    "libhoneycomb/browser/chrome_crash_reporter_client_stub.cc",
    "libhoneycomb/browser/chrome/extensions/chrome_mime_handler_view_guest_delegate_honey.cc",
    "libhoneycomb/browser/chrome/extensions/chrome_mime_handler_view_guest_delegate_honey.h",
    "libhoneycomb/browser/chrome/views/browser_platform_delegate_chrome_child_window.cc",
    "libhoneycomb/browser/chrome/views/browser_platform_delegate_chrome_child_window.h",
    "libhoneycomb/browser/chrome/views/browser_platform_delegate_chrome_views.cc",
    "libhoneycomb/browser/chrome/views/browser_platform_delegate_chrome_views.h",
    "libhoneycomb/browser/chrome/views/chrome_browser_frame.cc",
    "libhoneycomb/browser/chrome/views/chrome_browser_frame.h",
    "libhoneycomb/browser/chrome/views/chrome_browser_view.cc",
    "libhoneycomb/browser/chrome/views/chrome_browser_view.h",
    "libhoneycomb/browser/chrome/views/chrome_child_window.cc",
    "libhoneycomb/browser/chrome/views/chrome_child_window.h",
    "libhoneycomb/browser/chrome/views/chrome_views_util.cc",
    "libhoneycomb/browser/chrome/views/chrome_views_util.h",
    "libhoneycomb/browser/chrome/views/toolbar_view_impl.cc",
    "libhoneycomb/browser/chrome/views/toolbar_view_impl.h",
    "libhoneycomb/browser/chrome/views/toolbar_view_view.cc",
    "libhoneycomb/browser/chrome/views/toolbar_view_view.h",
    "libhoneycomb/browser/context.cc",
    "libhoneycomb/browser/context.h",
    "libhoneycomb/browser/context_menu_params_impl.cc",
    "libhoneycomb/browser/context_menu_params_impl.h",
    "libhoneycomb/browser/devtools/devtools_controller.cc",
    "libhoneycomb/browser/devtools/devtools_controller.h",
    "libhoneycomb/browser/devtools/devtools_file_manager.cc",
    "libhoneycomb/browser/devtools/devtools_file_manager.h",
    "libhoneycomb/browser/devtools/devtools_frontend.cc",
    "libhoneycomb/browser/devtools/devtools_frontend.h",
    "libhoneycomb/browser/devtools/devtools_manager.cc",
    "libhoneycomb/browser/devtools/devtools_manager.h",
    "libhoneycomb/browser/devtools/devtools_manager_delegate.cc",
    "libhoneycomb/browser/devtools/devtools_manager_delegate.h",
    "libhoneycomb/browser/download_item_impl.cc",
    "libhoneycomb/browser/download_item_impl.h",
    "libhoneycomb/browser/download_manager_delegate.cc",
    "libhoneycomb/browser/download_manager_delegate.h",
    "libhoneycomb/browser/extension_impl.cc",
    "libhoneycomb/browser/extension_impl.h",
    "libhoneycomb/browser/extensions/api/file_system/honey_file_system_delegate.cc",
    "libhoneycomb/browser/extensions/api/file_system/honey_file_system_delegate.h",
    "libhoneycomb/browser/extensions/api/storage/sync_value_store_cache.cc",
    "libhoneycomb/browser/extensions/api/storage/sync_value_store_cache.h",
    "libhoneycomb/browser/extensions/api/tabs/tabs_api.cc",
    "libhoneycomb/browser/extensions/api/tabs/tabs_api.h",
    "libhoneycomb/browser/extensions/alloy_extensions_util.cc",
    "libhoneycomb/browser/extensions/alloy_extensions_util.h",
    "libhoneycomb/browser/extensions/browser_extensions_util.cc",
    "libhoneycomb/browser/extensions/browser_extensions_util.h",
    "libhoneycomb/browser/extensions/browser_platform_delegate_background.cc",
    "libhoneycomb/browser/extensions/browser_platform_delegate_background.h",
    "libhoneycomb/browser/extensions/chrome_api_registration.cc",
    "libhoneycomb/browser/extensions/chrome_api_registration.h",
    "libhoneycomb/browser/extensions/component_extension_resource_manager.cc",
    "libhoneycomb/browser/extensions/component_extension_resource_manager.h",
    "libhoneycomb/browser/extensions/extensions_api_client.cc",
    "libhoneycomb/browser/extensions/extensions_api_client.h",
    "libhoneycomb/browser/extensions/extensions_browser_api_provider.cc",
    "libhoneycomb/browser/extensions/extensions_browser_api_provider.h",
    "libhoneycomb/browser/extensions/extensions_browser_client.cc",
    "libhoneycomb/browser/extensions/extensions_browser_client.h",
    "libhoneycomb/browser/extensions/extension_background_host.cc",
    "libhoneycomb/browser/extensions/extension_background_host.h",
    "libhoneycomb/browser/extensions/extension_function_details.cc",
    "libhoneycomb/browser/extensions/extension_function_details.h",
    "libhoneycomb/browser/extensions/extension_host_delegate.cc",
    "libhoneycomb/browser/extensions/extension_host_delegate.h",
    "libhoneycomb/browser/extensions/extension_system.cc",
    "libhoneycomb/browser/extensions/extension_system.h",
    "libhoneycomb/browser/extensions/extension_system_factory.cc",
    "libhoneycomb/browser/extensions/extension_system_factory.h",
    "libhoneycomb/browser/extensions/extension_view_host.cc",
    "libhoneycomb/browser/extensions/extension_view_host.h",
    "libhoneycomb/browser/extensions/extension_web_contents_observer.cc",
    "libhoneycomb/browser/extensions/extension_web_contents_observer.h",
    "libhoneycomb/browser/extensions/mime_handler_view_guest_delegate.cc",
    "libhoneycomb/browser/extensions/mime_handler_view_guest_delegate.h",
    "libhoneycomb/browser/extensions/value_store/honey_value_store.cc",
    "libhoneycomb/browser/extensions/value_store/honey_value_store.h",
    "libhoneycomb/browser/extensions/value_store/honey_value_store_factory.cc",
    "libhoneycomb/browser/extensions/value_store/honey_value_store_factory.h",
    "libhoneycomb/browser/file_dialog_manager.cc",
    "libhoneycomb/browser/file_dialog_manager.h",
    "libhoneycomb/browser/file_dialog_runner.cc",
    "libhoneycomb/browser/file_dialog_runner.h",
    "libhoneycomb/browser/frame_host_impl.cc",
    "libhoneycomb/browser/frame_host_impl.h",
    "libhoneycomb/browser/frame_service_base.h",
    "libhoneycomb/browser/global_preference_manager_impl.cc",
    "libhoneycomb/browser/global_preference_manager_impl.h",
    "libhoneycomb/browser/image_impl.cc",
    "libhoneycomb/browser/image_impl.h",
    "libhoneycomb/browser/iothread_state.cc",
    "libhoneycomb/browser/iothread_state.h",
    "libhoneycomb/browser/javascript_dialog_runner.h",
    "libhoneycomb/browser/javascript_dialog_manager.cc",
    "libhoneycomb/browser/javascript_dialog_manager.h",
    "libhoneycomb/browser/main_runner.cc",
    "libhoneycomb/browser/main_runner.h",
    "libhoneycomb/browser/media_access_query.cc",
    "libhoneycomb/browser/media_access_query.h",
    "libhoneycomb/browser/media_capture_devices_dispatcher.cc",
    "libhoneycomb/browser/media_capture_devices_dispatcher.h",
    "libhoneycomb/browser/media_router/media_route_impl.cc",
    "libhoneycomb/browser/media_router/media_route_impl.h",
    "libhoneycomb/browser/media_router/media_router_impl.cc",
    "libhoneycomb/browser/media_router/media_router_impl.h",
    "libhoneycomb/browser/media_router/media_router_manager.cc",
    "libhoneycomb/browser/media_router/media_router_manager.h",
    "libhoneycomb/browser/media_router/media_sink_impl.cc",
    "libhoneycomb/browser/media_router/media_sink_impl.h",
    "libhoneycomb/browser/media_router/media_source_impl.cc",
    "libhoneycomb/browser/media_router/media_source_impl.h",
    "libhoneycomb/browser/media_stream_registrar.cc",
    "libhoneycomb/browser/media_stream_registrar.h",
    "libhoneycomb/browser/menu_manager.cc",
    "libhoneycomb/browser/menu_manager.h",
    "libhoneycomb/browser/menu_model_impl.cc",
    "libhoneycomb/browser/menu_model_impl.h",
    "libhoneycomb/browser/menu_runner.h",
    "libhoneycomb/browser/native/browser_platform_delegate_native.cc",
    "libhoneycomb/browser/native/browser_platform_delegate_native.h",
    "libhoneycomb/browser/native/cursor_util.h",
    "libhoneycomb/browser/native/cursor_util.cc",
    "libhoneycomb/browser/native/window_delegate_view.cc",
    "libhoneycomb/browser/native/window_delegate_view.h",
    "libhoneycomb/browser/navigation_entry_impl.cc",
    "libhoneycomb/browser/navigation_entry_impl.h",
    "libhoneycomb/browser/net/chrome_scheme_handler.cc",
    "libhoneycomb/browser/net/chrome_scheme_handler.h",
    "libhoneycomb/browser/net/crlset_file_util_impl.cc",
    "libhoneycomb/browser/net/devtools_scheme_handler.cc",
    "libhoneycomb/browser/net/devtools_scheme_handler.h",
    "libhoneycomb/browser/net/internal_scheme_handler.cc",
    "libhoneycomb/browser/net/internal_scheme_handler.h",
    "libhoneycomb/browser/net/scheme_handler.cc",
    "libhoneycomb/browser/net/scheme_handler.h",
    "libhoneycomb/browser/net/throttle_handler.cc",
    "libhoneycomb/browser/net/throttle_handler.h",
    "libhoneycomb/browser/net_service/browser_urlrequest_impl.cc",
    "libhoneycomb/browser/net_service/browser_urlrequest_impl.h",
    "libhoneycomb/browser/net_service/cookie_helper.cc",
    "libhoneycomb/browser/net_service/cookie_helper.h",
    "libhoneycomb/browser/net_service/cookie_manager_impl.cc",
    "libhoneycomb/browser/net_service/cookie_manager_impl.h",
    "libhoneycomb/browser/net_service/login_delegate.cc",
    "libhoneycomb/browser/net_service/login_delegate.h",
    "libhoneycomb/browser/net_service/proxy_url_loader_factory.cc",
    "libhoneycomb/browser/net_service/proxy_url_loader_factory.h",
    "libhoneycomb/browser/net_service/resource_handler_wrapper.cc",
    "libhoneycomb/browser/net_service/resource_handler_wrapper.h",
    "libhoneycomb/browser/net_service/resource_request_handler_wrapper.cc",
    "libhoneycomb/browser/net_service/resource_request_handler_wrapper.h",
    "libhoneycomb/browser/net_service/response_filter_wrapper.cc",
    "libhoneycomb/browser/net_service/response_filter_wrapper.h",
    "libhoneycomb/browser/net_service/stream_reader_url_loader.cc",
    "libhoneycomb/browser/net_service/stream_reader_url_loader.h",
    "libhoneycomb/browser/net_service/url_loader_factory_getter.cc",
    "libhoneycomb/browser/net_service/url_loader_factory_getter.h",
    "libhoneycomb/browser/origin_whitelist_impl.cc",
    "libhoneycomb/browser/origin_whitelist_impl.h",
    "libhoneycomb/browser/osr/browser_platform_delegate_osr.cc",
    "libhoneycomb/browser/osr/browser_platform_delegate_osr.h",
    "libhoneycomb/browser/osr/host_display_client_osr.cc",
    "libhoneycomb/browser/osr/host_display_client_osr.h",
    "libhoneycomb/browser/osr/motion_event_osr.cc",
    "libhoneycomb/browser/osr/motion_event_osr.h",
    "libhoneycomb/browser/osr/osr_accessibility_util.cc",
    "libhoneycomb/browser/osr/osr_accessibility_util.h",
    "libhoneycomb/browser/osr/osr_util.cc",
    "libhoneycomb/browser/osr/osr_util.h",
    "libhoneycomb/browser/osr/render_widget_host_view_osr.cc",
    "libhoneycomb/browser/osr/render_widget_host_view_osr.h",
    "libhoneycomb/browser/osr/synthetic_gesture_target_osr.cc",
    "libhoneycomb/browser/osr/synthetic_gesture_target_osr.h",
    "libhoneycomb/browser/osr/touch_handle_drawable_osr.cc",
    "libhoneycomb/browser/osr/touch_handle_drawable_osr.h",
    "libhoneycomb/browser/osr/touch_selection_controller_client_osr.cc",
    "libhoneycomb/browser/osr/touch_selection_controller_client_osr.h",
    "libhoneycomb/browser/osr/video_consumer_osr.cc",
    "libhoneycomb/browser/osr/video_consumer_osr.h",
    "libhoneycomb/browser/osr/web_contents_view_osr.cc",
    "libhoneycomb/browser/osr/web_contents_view_osr.h",
    "libhoneycomb/browser/path_util_impl.cc",
    "libhoneycomb/browser/permission_prompt.cc",
    "libhoneycomb/browser/permission_prompt.h",
    "libhoneycomb/browser/prefs/browser_prefs.cc",
    "libhoneycomb/browser/prefs/browser_prefs.h",
    "libhoneycomb/browser/prefs/pref_helper.cc",
    "libhoneycomb/browser/prefs/pref_helper.h",
    "libhoneycomb/browser/prefs/pref_registrar.cc",
    "libhoneycomb/browser/prefs/pref_registrar.h",
    "libhoneycomb/browser/prefs/pref_store.cc",
    "libhoneycomb/browser/prefs/pref_store.h",
    "libhoneycomb/browser/prefs/renderer_prefs.cc",
    "libhoneycomb/browser/prefs/renderer_prefs.h",
    "libhoneycomb/browser/print_settings_impl.cc",
    "libhoneycomb/browser/print_settings_impl.h",
    "libhoneycomb/browser/printing/print_util.cc",
    "libhoneycomb/browser/printing/print_util.h",
    "libhoneycomb/browser/process_util_impl.cc",
    "libhoneycomb/browser/request_context_handler_map.cc",
    "libhoneycomb/browser/request_context_handler_map.h",
    "libhoneycomb/browser/request_context_impl.cc",
    "libhoneycomb/browser/request_context_impl.h",
    "libhoneycomb/browser/scheme_impl.cc",
    "libhoneycomb/browser/server_impl.cc",
    "libhoneycomb/browser/server_impl.h",
    "libhoneycomb/browser/simple_menu_model_impl.cc",
    "libhoneycomb/browser/simple_menu_model_impl.h",
    "libhoneycomb/browser/speech_recognition_manager_delegate.cc",
    "libhoneycomb/browser/speech_recognition_manager_delegate.h",
    "libhoneycomb/browser/ssl_host_state_delegate.cc",
    "libhoneycomb/browser/ssl_host_state_delegate.h",
    "libhoneycomb/browser/ssl_info_impl.cc",
    "libhoneycomb/browser/ssl_info_impl.h",
    "libhoneycomb/browser/ssl_status_impl.cc",
    "libhoneycomb/browser/ssl_status_impl.h",
    "libhoneycomb/browser/stream_impl.cc",
    "libhoneycomb/browser/stream_impl.h",
    "libhoneycomb/browser/trace_impl.cc",
    "libhoneycomb/browser/trace_subscriber.cc",
    "libhoneycomb/browser/trace_subscriber.h",
    "libhoneycomb/browser/thread_util.h",
    "libhoneycomb/browser/views/basic_label_button_impl.cc",
    "libhoneycomb/browser/views/basic_label_button_impl.h",
    "libhoneycomb/browser/views/basic_label_button_view.cc",
    "libhoneycomb/browser/views/basic_label_button_view.h",
    "libhoneycomb/browser/views/basic_panel_impl.cc",
    "libhoneycomb/browser/views/basic_panel_impl.h",
    "libhoneycomb/browser/views/basic_panel_view.cc",
    "libhoneycomb/browser/views/basic_panel_view.h",
    "libhoneycomb/browser/views/box_layout_impl.cc",
    "libhoneycomb/browser/views/box_layout_impl.h",
    "libhoneycomb/browser/views/browser_platform_delegate_views.cc",
    "libhoneycomb/browser/views/browser_platform_delegate_views.h",
    "libhoneycomb/browser/views/browser_view_impl.cc",
    "libhoneycomb/browser/views/browser_view_impl.h",
    "libhoneycomb/browser/views/browser_view_view.cc",
    "libhoneycomb/browser/views/browser_view_view.h",
    "libhoneycomb/browser/views/button_impl.h",
    "libhoneycomb/browser/views/button_view.h",
    "libhoneycomb/browser/views/display_impl.cc",
    "libhoneycomb/browser/views/display_impl.h",
    "libhoneycomb/browser/views/fill_layout_impl.cc",
    "libhoneycomb/browser/views/fill_layout_impl.h",
    "libhoneycomb/browser/views/label_button_impl.h",
    "libhoneycomb/browser/views/label_button_view.h",
    "libhoneycomb/browser/views/layout_impl.h",
    "libhoneycomb/browser/views/layout_adapter.cc",
    "libhoneycomb/browser/views/layout_adapter.h",
    "libhoneycomb/browser/views/layout_util.cc",
    "libhoneycomb/browser/views/layout_util.h",
    "libhoneycomb/browser/views/menu_button_impl.cc",
    "libhoneycomb/browser/views/menu_button_impl.h",
    "libhoneycomb/browser/views/menu_button_view.cc",
    "libhoneycomb/browser/views/menu_button_view.h",
    "libhoneycomb/browser/views/menu_runner_views.cc",
    "libhoneycomb/browser/views/menu_runner_views.h",
    "libhoneycomb/browser/views/overlay_view_host.cc",
    "libhoneycomb/browser/views/overlay_view_host.h",
    "libhoneycomb/browser/views/panel_impl.h",
    "libhoneycomb/browser/views/panel_view.h",
    "libhoneycomb/browser/views/scroll_view_impl.cc",
    "libhoneycomb/browser/views/scroll_view_impl.h",
    "libhoneycomb/browser/views/scroll_view_view.cc",
    "libhoneycomb/browser/views/scroll_view_view.h",
    "libhoneycomb/browser/views/textfield_impl.cc",
    "libhoneycomb/browser/views/textfield_impl.h",
    "libhoneycomb/browser/views/textfield_view.cc",
    "libhoneycomb/browser/views/textfield_view.h",
    "libhoneycomb/browser/views/view_adapter.cc",
    "libhoneycomb/browser/views/view_adapter.h",
    "libhoneycomb/browser/views/view_impl.h",
    "libhoneycomb/browser/views/view_util.cc",
    "libhoneycomb/browser/views/view_util.h",
    "libhoneycomb/browser/views/view_view.h",
    "libhoneycomb/browser/views/widget_destruction_observer.h",
    "libhoneycomb/browser/views/window_impl.cc",
    "libhoneycomb/browser/views/window_impl.h",
    "libhoneycomb/browser/views/window_view.cc",
    "libhoneycomb/browser/views/window_view.h",
    "libhoneycomb/browser/x509_certificate_impl.cc",
    "libhoneycomb/browser/x509_certificate_impl.h",
    "libhoneycomb/browser/x509_cert_principal_impl.cc",
    "libhoneycomb/browser/x509_cert_principal_impl.h",
    "libhoneycomb/browser/xml_reader_impl.cc",
    "libhoneycomb/browser/xml_reader_impl.h",
    "libhoneycomb/browser/zip_reader_impl.cc",
    "libhoneycomb/browser/zip_reader_impl.h",
    "libhoneycomb/common/alloy/alloy_content_client.cc",
    "libhoneycomb/common/alloy/alloy_content_client.h",
    "libhoneycomb/common/alloy/alloy_main_delegate.cc",
    "libhoneycomb/common/alloy/alloy_main_delegate.h",
    "libhoneycomb/common/alloy/alloy_main_runner_delegate.cc",
    "libhoneycomb/common/alloy/alloy_main_runner_delegate.h",
    "libhoneycomb/common/app_manager.cc",
    "libhoneycomb/common/app_manager.h",
    "libhoneycomb/common/base_impl.cc",
    "libhoneycomb/common/honey_switches.cc",
    "libhoneycomb/common/honey_switches.h",
    "libhoneycomb/common/chrome/chrome_content_client_honey.cc",
    "libhoneycomb/common/chrome/chrome_content_client_honey.h",
    "libhoneycomb/common/chrome/chrome_main_delegate_honey.cc",
    "libhoneycomb/common/chrome/chrome_main_delegate_honey.h",
    "libhoneycomb/common/chrome/chrome_main_runner_delegate.cc",
    "libhoneycomb/common/chrome/chrome_main_runner_delegate.h",
    "libhoneycomb/common/command_line_impl.cc",
    "libhoneycomb/common/command_line_impl.h",
    "libhoneycomb/common/crash_reporter_client.cc",
    "libhoneycomb/common/crash_reporter_client.h",
    "libhoneycomb/common/crash_reporting.cc",
    "libhoneycomb/common/crash_reporting.h",
    "libhoneycomb/common/drag_data_impl.cc",
    "libhoneycomb/common/drag_data_impl.h",
    "libhoneycomb/common/extensions/chrome_generated_schemas.cc",
    "libhoneycomb/common/extensions/chrome_generated_schemas.h",
    "libhoneycomb/common/extensions/extensions_api_provider.cc",
    "libhoneycomb/common/extensions/extensions_api_provider.h",
    "libhoneycomb/common/extensions/extensions_client.cc",
    "libhoneycomb/common/extensions/extensions_client.h",
    "libhoneycomb/common/extensions/extensions_util.cc",
    "libhoneycomb/common/extensions/extensions_util.h",
    "libhoneycomb/common/file_util_impl.cc",
    "libhoneycomb/common/frame_util.cc",
    "libhoneycomb/common/frame_util.h",
    "libhoneycomb/common/i18n_util_impl.cc",
    "libhoneycomb/common/json_impl.cc",
    "libhoneycomb/common/main_runner_delegate.h",
    "libhoneycomb/common/main_runner_handler.h",
    "libhoneycomb/common/net/http_header_utils.cc",
    "libhoneycomb/common/net/http_header_utils.h",
    "libhoneycomb/common/net/net_resource_provider.cc",
    "libhoneycomb/common/net/net_resource_provider.h",
    "libhoneycomb/common/net/scheme_registration.cc",
    "libhoneycomb/common/net/scheme_registration.h",
    "libhoneycomb/common/net/url_util.cc",
    "libhoneycomb/common/net/url_util.h",
    "libhoneycomb/common/net_service/net_service_util.cc",
    "libhoneycomb/common/net_service/net_service_util.h",
    "libhoneycomb/common/parser_impl.cc",
    "libhoneycomb/common/process_message_impl.cc",
    "libhoneycomb/common/process_message_impl.h",
    "libhoneycomb/common/process_message_smr_impl.cc",
    "libhoneycomb/common/process_message_smr_impl.h",
    "libhoneycomb/common/request_impl.cc",
    "libhoneycomb/common/request_impl.h",
    "libhoneycomb/common/resource_bundle_delegate.cc",
    "libhoneycomb/common/resource_bundle_delegate.h",
    "libhoneycomb/common/resource_bundle_impl.cc",
    "libhoneycomb/common/resource_bundle_impl.h",
    "libhoneycomb/common/resource_util.cc",
    "libhoneycomb/common/resource_util.h",
    "libhoneycomb/common/response_impl.cc",
    "libhoneycomb/common/response_impl.h",
    "libhoneycomb/common/scheme_registrar_impl.cc",
    "libhoneycomb/common/scheme_registrar_impl.h",
    "libhoneycomb/common/string_list_impl.cc",
    "libhoneycomb/common/string_map_impl.cc",
    "libhoneycomb/common/string_multimap_impl.cc",
    "libhoneycomb/common/string_types_impl.cc",
    "libhoneycomb/common/string_util.cc",
    "libhoneycomb/common/string_util.h",
    "libhoneycomb/common/task_impl.cc",
    "libhoneycomb/common/task_runner_impl.cc",
    "libhoneycomb/common/task_runner_impl.h",
    "libhoneycomb/common/task_runner_manager.cc",
    "libhoneycomb/common/task_runner_manager.h",
    "libhoneycomb/common/thread_impl.cc",
    "libhoneycomb/common/thread_impl.h",
    "libhoneycomb/common/time_impl.cc",
    "libhoneycomb/common/time_util.h",
    "libhoneycomb/common/tracker.cc",
    "libhoneycomb/common/tracker.h",
    "libhoneycomb/common/urlrequest_impl.cc",
    "libhoneycomb/common/value_base.cc",
    "libhoneycomb/common/value_base.h",
    "libhoneycomb/common/values_impl.cc",
    "libhoneycomb/common/values_impl.h",
    "libhoneycomb/common/waitable_event_impl.cc",
    "libhoneycomb/common/waitable_event_impl.h",
    "libhoneycomb/features/runtime.h",
    "libhoneycomb/features/runtime_checks.h",
    "libhoneycomb/renderer/alloy/alloy_content_renderer_client.cc",
    "libhoneycomb/renderer/alloy/alloy_content_renderer_client.h",
    "libhoneycomb/renderer/alloy/alloy_render_thread_observer.cc",
    "libhoneycomb/renderer/alloy/alloy_render_thread_observer.h",
    "libhoneycomb/renderer/alloy/url_loader_throttle_provider_impl.cc",
    "libhoneycomb/renderer/alloy/url_loader_throttle_provider_impl.h",
    "libhoneycomb/renderer/browser_impl.cc",
    "libhoneycomb/renderer/browser_impl.h",
    "libhoneycomb/renderer/chrome/chrome_content_renderer_client_honey.cc",
    "libhoneycomb/renderer/chrome/chrome_content_renderer_client_honey.h",
    "libhoneycomb/renderer/dom_document_impl.cc",
    "libhoneycomb/renderer/dom_document_impl.h",
    "libhoneycomb/renderer/dom_node_impl.cc",
    "libhoneycomb/renderer/dom_node_impl.h",
    "libhoneycomb/renderer/extensions/extensions_dispatcher_delegate.cc",
    "libhoneycomb/renderer/extensions/extensions_dispatcher_delegate.h",
    "libhoneycomb/renderer/extensions/extensions_renderer_client.cc",
    "libhoneycomb/renderer/extensions/extensions_renderer_client.h",
    "libhoneycomb/renderer/extensions/print_render_frame_helper_delegate.cc",
    "libhoneycomb/renderer/extensions/print_render_frame_helper_delegate.h",
    "libhoneycomb/renderer/frame_impl.cc",
    "libhoneycomb/renderer/frame_impl.h",
    "libhoneycomb/renderer/render_frame_observer.cc",
    "libhoneycomb/renderer/render_frame_observer.h",
    "libhoneycomb/renderer/render_frame_util.cc",
    "libhoneycomb/renderer/render_frame_util.h",
    "libhoneycomb/renderer/render_manager.cc",
    "libhoneycomb/renderer/render_manager.h",
    "libhoneycomb/renderer/thread_util.h",
    "libhoneycomb/renderer/v8_impl.cc",
    "libhoneycomb/renderer/v8_impl.h",

    # For Chrome runtime support.
    "//chrome/app/chrome_main_delegate.cc",
    "//chrome/app/chrome_main_delegate.h",
  ]

  configs += [
    "libhoneycomb/features:config",
    "//build/config:precompiled_headers",
  ]

  public_configs = [
    "libhoneycomb/features:config",
  ]

  include_dirs = [
    # Crashpad code uses paths relative to this directory.
    "//third_party/crashpad/crashpad",
  ]

  public_deps = [
    # Bring in feature flag defines.
    "//honey/libhoneycomb/features",
    # Support relative include paths.
    "//third_party/abseil-cpp:absl",
  ]

  deps = [
    ":honey_make_headers",
    "libhoneycomb/common/mojom",

    ":libhoneycomb_static_unittested",

    # Generate API bindings for extensions.
    # TODO(honey): Enable if/when Honeycomb exposes its own Mojo APIs. See
    # libhoneycomb/common/extensions/api/README.txt for details.
    #"libhoneycomb/common/extensions/api",
    #"libhoneycomb/common/extensions/api:api_registration",
    "libhoneycomb/common/extensions/api:extensions_features",

    # Normal build dependencies. Should be sorted alphabetically.
    "//base",
    "//base:base_static",
    "//base/third_party/dynamic_annotations",
    "//cc",
    "//chrome:dependencies",
    "//chrome:packed_resources",
    "//chrome:resources",
    "//chrome:strings",
    "//chrome/common:buildflags",
    "//chrome/services/printing:lib",
    "//components/cdm/renderer",
    "//components/certificate_transparency",
    "//components/component_updater",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/crx_file",
    "//components/google/core/common",
    "//components/keyed_service/content:content",
    "//components/keyed_service/core:core",
    "//components/media_router/common/mojom:media_router",
    "//components/navigation_interception",
    "//components/network_session_configurator/common",
    "//components/pdf/browser",
    "//components/pdf/renderer",
    "//components/plugins/renderer",
    "//components/pref_registry",
    "//components/printing/browser",
    "//components/printing/common",
    "//components/printing/renderer",
    "//components/proxy_config",
    "//components/services/print_compositor/public/cpp",
    "//components/services/print_compositor/public/mojom",
    "//components/update_client",
    "//components/url_formatter",
    "//components/user_prefs",
    "//components/version_info",
    "//components/visitedlink/browser",
    "//components/visitedlink/common",
    "//components/visitedlink/renderer",
    "//components/viz/service",
    "//components/web_cache/renderer",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/child",
    "//content/public/common",
    "//content/public/gpu",
    "//content/public/renderer",
    "//content/public/utility",
    "//crypto",
    "//device/base",
    "//extensions/browser",
    "//extensions/buildflags",
    "//extensions/common/api",
    "//extensions/common:core_api_provider",
    "//extensions/renderer",
    "//gpu",
    "//ipc",
    "//media",
    "//net",
    "//pdf",
    "//ppapi/buildflags",
    "//printing/buildflags",
    "//services/network:network_service",
    "//services/network/public/cpp",
    "//services/service_manager/public/cpp",
    "//skia",
    "//storage/browser",
    "//third_party/blink/public:blink",
    "//third_party/brotli:dec",
    "//third_party/cld_3/src/src:cld_3",
    "//third_party/crashpad/crashpad/handler",
    "//third_party/hunspell",
    "//third_party/leveldatabase",
    "//third_party/libxml:libxml",
    "//third_party/widevine/cdm:headers",
    "//third_party/widevine/cdm",
    "//third_party/icu",
    "//third_party/zlib:minizip",
    "//ui/base",
    "//ui/base/ime",
    "//ui/events",
    "//ui/events:events_base",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/ipc",
    "//ui/gfx/ipc/geometry",
    "//ui/gfx/ipc/skia",
    "//ui/gl",
    "//ui/strings",
    "//ui/views",
    "//ui/views/controls/webview",
    "//url",
    "//v8",
  ]

  if (is_win) {
    sources += includes_win + [
      "libhoneycomb/browser/alloy/alloy_browser_main_win.cc",
      "libhoneycomb/browser/native/browser_platform_delegate_native_win.cc",
      "libhoneycomb/browser/native/browser_platform_delegate_native_win.h",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_win.cc",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_win.h",
    ]

    deps += [
      "//chrome/install_static:secondary_module",
      "//chrome/chrome_elf",
    ]

    if (is_component_build) {
      deps += [ "//content:sandbox_helper_win" ]
    }

    libs = [
      "comctl32.lib",
      # For D3D11_DECODER_PROFILE_H264_VLD_NOFGT.
      "dxguid.lib",
    ]

    data_deps = [
      "//chrome/elevation_service",
    ]
  }

  if (is_linux) {
    sources += includes_linux + [
      "libhoneycomb/browser/native/browser_platform_delegate_native_linux.cc",
      "libhoneycomb/browser/native/browser_platform_delegate_native_linux.h",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_linux.cc",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_linux.h",
      "libhoneycomb/browser/printing/print_dialog_linux.cc",
      "libhoneycomb/browser/printing/print_dialog_linux.h",
      "libhoneycomb/common/util_linux.h",
      "libhoneycomb/common/util_linux.cc",
    ]

    if (ozone_platform_x11) {
      sources += [
        "libhoneycomb/browser/native/window_x11.cc",
        "libhoneycomb/browser/native/window_x11.h",
      ]
    }

    deps += [
      "//build/config/freetype",
      "//third_party/fontconfig",
    ]

    if (is_linux && !ozone_platform_x11) {
      deps += [
        "//third_party/angle:libEGL",
      ]
    }
  }

  if (is_mac) {
    sources += includes_mac + [
      "libhoneycomb/browser/native/browser_platform_delegate_native_mac.h",
      "libhoneycomb/browser/native/browser_platform_delegate_native_mac.mm",
      "libhoneycomb/browser/native/cursor_util_mac.mm",
      "libhoneycomb/browser/native/javascript_dialog_runner_mac.h",
      "libhoneycomb/browser/native/javascript_dialog_runner_mac.mm",
      "libhoneycomb/browser/native/menu_runner_mac.h",
      "libhoneycomb/browser/native/menu_runner_mac.mm",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_mac.h",
      "libhoneycomb/browser/osr/browser_platform_delegate_osr_mac.mm",
      "libhoneycomb/browser/views/native_widget_mac.h",
      "libhoneycomb/browser/views/native_widget_mac.mm",
      "libhoneycomb/browser/views/ns_window.h",
      "libhoneycomb/browser/views/ns_window.mm",
      "libhoneycomb/browser/views/view_util_mac.mm",
      "libhoneycomb/common/util_mac.h",
      "libhoneycomb/common/util_mac.mm",

      # For Chrome runtime support.
      "//chrome/app/chrome_main_mac.h",
      "//chrome/app/chrome_main_mac.mm",
    ]
  }

  if (ozone_platform_x11) {
    deps += [ "//ui/events/devices/x11" ]
  }

  if (v8_use_external_startup_data && use_v8_context_snapshot) {
    deps += [ "//tools/v8_context_snapshot" ]
  }

  if (use_aura) {
    sources += [
      "libhoneycomb/browser/native/browser_platform_delegate_native_aura.cc",
      "libhoneycomb/browser/native/browser_platform_delegate_native_aura.h",
      "libhoneycomb/browser/native/cursor_util_aura.cc",
      "libhoneycomb/browser/native/menu_runner_views_aura.cc",
      "libhoneycomb/browser/native/menu_runner_views_aura.h",
      "libhoneycomb/browser/views/view_util_aura.cc",
    ]

    deps += [
      "//ui/aura",
      "//ui/wm",
      "//ui/wm/public",
    ]
  }

  if (enable_cdm_host_verification) {
    sources += [
      "libhoneycomb/common/cdm_host_file_path.cc",
      "libhoneycomb/common/cdm_host_file_path.h",
    ]
  }
}


#
# libhoneycomb_dll_wrapper static targets.
#

# Configuration that will be applied to all targets that build autogen files.
config("libhoneycomb_autogen_config") {
  if (is_clang) {
    cflags = [
      # Disable clang warnings related to Honeycomb's translation layer templates.
      "-Wno-undefined-var-template",
    ]
  }
}

# Configuration that will be applied to all targets that depend on
# libhoneycomb_dll_wrapper.
config("libhoneycomb_dll_wrapper_config") {
  include_dirs = [
    # Honeycomb sources use include paths relative to the Honeycomb root directory.
    ".",
    # Honeycomb generates some header files that also need to be discoverable.
    # They will be copied to the include/ directory in the binary distribution.
    "$root_out_dir/includes",
  ]

  configs = [ ":libhoneycomb_autogen_config" ]

  if (is_win) {
    if (current_cpu == "x86") {
      # Set the initial stack size to 0.5MiB, instead of the 1.5MiB minimum
      # needed by Honeycomb's main thread. This saves significant memory on threads
      # (like those in the Windows thread pool, and others) whose stack size we
      # can only control through this setting. The main thread (in 32-bit builds
      # only) uses fibers to switch to a 4MiB stack at runtime via
      # HoneycombRunWinMainWithPreferredStackSize().
      ldflags = [ "/STACK:0x80000" ]
    } else {
      # Increase the initial stack size to 8MiB from the default 1MiB.
      ldflags = [ "/STACK:0x800000" ]
    }
  }
}

# libhoneycomb_dll_wrapper target.
static_library("libhoneycomb_dll_wrapper") {
  sources = includes_common +
            gypi_paths.autogen_cpp_includes +
            gypi_paths2.includes_capi +
            gypi_paths.autogen_capi_includes +
            gypi_paths2.includes_wrapper +
            gypi_paths2.libhoneycomb_dll_wrapper_sources_base +
            gypi_paths2.libhoneycomb_dll_wrapper_sources_common +
            gypi_paths.autogen_client_side

  if (is_mac) {
    sources += gypi_paths2.libhoneycomb_dll_wrapper_sources_mac
  }

  defines = [ "WRAPPING_HONEYCOMB_SHARED" ]

  configs += [ ":libhoneycomb_dll_wrapper_config" ]
  public_configs = [ ":libhoneycomb_dll_wrapper_config" ]

  deps = [ ":honey_make_headers" ]
}


#
# honey_sandbox target.
#

if (is_win) {
  static_library("honey_sandbox") {
    sources = [ "libhoneycomb_dll/sandbox/sandbox_win.cc" ]
    # Honeycomb sources use include paths relative to the Honeycomb root directory.
    include_dirs = [ "." ]
    deps = [ "libhoneycomb/features", "//sandbox" ]
  }
}

if (is_mac) {
  static_library("honey_sandbox") {
    sources = [ "libhoneycomb_dll/sandbox/sandbox_mac.mm" ]
    # Honeycomb sources use include paths relative to the Honeycomb root directory.
    include_dirs = [ "." ]
    deps = [
      "//build/config:executable_deps",
      "//sandbox/mac:seatbelt"
    ]
  }
}

#
# Resource grit/pack targets.
#

# Included in locales/*.pak via //chrome/chrome_repack_locales.gni.
grit("honey_strings") {
  source = "libhoneycomb/resources/honey_strings.grd"
  outputs = [
    "grit/honey_strings.h",
  ]
  all_locales = platform_pak_locales + [ "fake-bidi" ]
  foreach(locale, all_locales) {
    outputs += [ "honey_strings_${locale}.pak" ]
  }
}

# Included in resources.pak via //chrome/chrome_paks.gni.
grit("honey_resources") {
  source = "libhoneycomb/resources/honey_resources.grd"
  outputs = [
    "grit/honey_resources.h",
    "honey_resources.pak",
  ]
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]
}

# Helper for generating pack header files.
template("make_pack_header") {
  assert(defined(invoker.header))
  assert(defined(invoker.inputs))

  action("make_pack_header_${target_name}") {
    script = "tools/make_pack_header.py"

    inputs = invoker.inputs
    outputs = [ invoker.header ]

    args = rebase_path(outputs, root_build_dir) +
           rebase_path(inputs, root_build_dir)

    if (defined(invoker.deps)) {
      deps = invoker.deps
    }
  }
}

# Generate honey_pack_resources.h.
make_pack_header("resources") {
  header = "$root_out_dir/includes/include/honey_pack_resources.h"
  inputs = [
    "$root_gen_dir/base/tracing/protos/grit/tracing_proto_resources.h",
    "$root_gen_dir/honey/grit/honey_resources.h",
    "$root_gen_dir/chrome/grit/browser_resources.h",
    "$root_gen_dir/chrome/grit/common_resources.h",
    "$root_gen_dir/chrome/grit/component_extension_resources.h",
    "$root_gen_dir/chrome/grit/dev_ui_browser_resources.h",
    "$root_gen_dir/chrome/grit/pdf_resources.h",
    "$root_gen_dir/chrome/grit/renderer_resources.h",
    "$root_gen_dir/components/grit/components_resources.h",
    "$root_gen_dir/components/grit/dev_ui_components_resources.h",
    "$root_gen_dir/content/browser/devtools/grit/devtools_resources.h",
    "$root_gen_dir/content/browser/tracing/grit/tracing_resources.h",
    "$root_gen_dir/content/browser/webrtc/resources/grit/webrtc_internals_resources.h",
    "$root_gen_dir/content/grit/content_resources.h",
    "$root_gen_dir/content/grit/gpu_resources.h",
    "$root_gen_dir/content/grit/histograms_resources.h",
    "$root_gen_dir/content/grit/process_resources.h",
    "$root_gen_dir/content/grit/service_worker_resources.h",
    "$root_gen_dir/extensions/grit/extensions_browser_resources.h",
    "$root_gen_dir/extensions/grit/extensions_renderer_resources.h",
    "$root_gen_dir/extensions/grit/extensions_resources.h",
    "$root_gen_dir/mojo/public/js/grit/mojo_bindings_resources.h",
    "$root_gen_dir/net/grit/net_resources.h",
    "$root_gen_dir/third_party/blink/public/resources/grit/blink_resources.h",
    "$root_gen_dir/ui/resources/grit/ui_resources.h",
    "$root_gen_dir/ui/resources/grit/webui_resources.h",
    "$root_gen_dir/ui/views/resources/grit/views_resources.h",
  ]

  deps = [
    ":honey_resources",
    "//base/tracing/protos:chrome_track_event_resources",
    "//chrome/browser:dev_ui_browser_resources",
    "//chrome/browser:resources",
    "//chrome/browser/resources:component_extension_resources",
    "//chrome/browser/resources/pdf:resources",
    "//chrome/common:resources",
    "//chrome/renderer:resources",
    "//components/resources:components_resources",
    "//components/resources:dev_ui_components_resources",
    "//content/browser/devtools:devtools_resources",
    "//content/browser/resources/gpu:resources",
    "//content/browser/resources/histograms:resources",
    "//content/browser/resources/process:resources",
    "//content/browser/resources/service_worker:resources",
    "//content/browser/tracing:resources",
    "//content/browser/webrtc/resources",
    "//content:content_resources",
    "//extensions:extensions_browser_resources",
    "//extensions:extensions_renderer_resources",
    "//extensions:extensions_resources_grd",
    "//mojo/public/js:resources",
    "//net:net_resources",
    "//third_party/blink/public:resources",
    "//ui/resources:ui_resources_grd",
    "//ui/resources:webui_resources_grd",
    "//ui/views/resources:resources_grd",
  ]
}

# Generate honey_pack_strings.h.
make_pack_header("strings") {
  header = "$root_out_dir/includes/include/honey_pack_strings.h"
  inputs = [
    "$root_gen_dir/honey/grit/honey_strings.h",
    "$root_gen_dir/chrome/grit/chromium_strings.h",
    "$root_gen_dir/chrome/grit/generated_resources.h",
    "$root_gen_dir/chrome/grit/locale_settings.h",
    "$root_gen_dir/chrome/grit/platform_locale_settings.h",
    "$root_gen_dir/components/omnibox/resources/grit/omnibox_pedal_synonyms.h",
    "$root_gen_dir/components/strings/grit/components_chromium_strings.h",
    "$root_gen_dir/components/strings/grit/components_strings.h",
    "$root_gen_dir/extensions/strings/grit/extensions_strings.h",
    "$root_gen_dir/services/strings/grit/services_strings.h",
    "$root_gen_dir/third_party/blink/public/strings/grit/blink_accessibility_strings.h",
    "$root_gen_dir/third_party/blink/public/strings/grit/blink_strings.h",
    "$root_gen_dir/ui/strings/grit/ui_strings.h",
  ]

  deps = [
    ":honey_strings",
    "//chrome/app:chromium_strings",
    "//chrome/app:generated_resources",
    "//chrome/app/resources:locale_settings",
    "//chrome/app/resources:platform_locale_settings",
    "//components/omnibox/resources:omnibox_pedal_synonyms",
    "//components/strings:components_chromium_strings",
    "//components/strings:components_locale_settings",
    "//components/strings:components_strings",
    "//extensions/strings",
    "//services/strings",
    "//third_party/blink/public/strings",
    "//third_party/blink/public/strings:accessibility_strings",
    "//ui/strings:app_locale_settings",
    "//ui/strings:ui_strings",
  ]
}

# Generate honey_command_ids.h.
make_pack_header("command_ids") {
  header = "$root_out_dir/includes/include/honey_command_ids.h"
  inputs = [
    "//chrome/app/chrome_command_ids.h",
  ]
}

# Generate honey_api_hash.h.
action("make_api_hash_header") {
  script = "tools/make_api_hash_header.py"

  # List of all C API files that will be checked for changes by honey_api_hash.py.
  inputs = gypi_paths2.includes_common_capi +
           gypi_paths2.includes_linux_capi +
           gypi_paths2.includes_mac_capi +
           gypi_paths2.includes_win_capi +
           gypi_paths2.includes_capi +
           gypi_paths.autogen_capi_includes
  include_dir = [ "include" ]
  outputs = [ "$root_out_dir/includes/include/honey_api_hash.h" ]

  args = rebase_path(outputs + include_dir, root_build_dir)
}

# Generate honey_config.h.
action("make_config_header") {
  script = "tools/make_config_header.py"

  outputs = [ "$root_out_dir/includes/include/honey_config.h" ]

  args = rebase_path(outputs + [ "$root_out_dir/args.gn" ], root_build_dir)
}

# Generate pack files and associated Honeycomb header files.
group("honey_make_headers") {
  deps = [
    ":make_pack_header_resources",
    ":make_pack_header_strings",
    ":make_pack_header_command_ids",
    ":make_api_hash_header",
    ":make_config_header",
  ]
}


#
# libhoneycomb dll/framework target.
#

if (is_mac) {
  honey_framework_name = "Honeycomb"

  tweak_info_plist("honey_framework_plist") {
    info_plist = "libhoneycomb/resources/framework-Info.plist"
    args = [
      "--breakpad=0",
      "--keystone=0",
      "--scm=1",
      "--version",
      honey_plist_version,
      "--branding",
      honey_framework_name,
    ]
  }

  bundle_data("honey_framework_resources") {
    sources = []
    public_deps = []

    if (icu_use_data_file) {
      sources += [ "$root_out_dir/icudtl.dat" ]
      public_deps += [ "//third_party/icu:icudata", ]
    }

    sources += [ "//ui/gl/resources/angle-metal/gpu_shader_cache.bin" ]

    if (v8_use_external_startup_data) {
      sources += [
        "$root_out_dir/snapshot_blob.bin",
      ]
      public_deps += [ "//v8" ]
      if (use_v8_context_snapshot) {
        sources += [ "$root_out_dir/$v8_context_snapshot_filename" ]
        public_deps += [ "//tools/v8_context_snapshot" ]
      }
    }

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }

  if (use_egl) {
    # Add the ANGLE .dylibs in the MODULE_DIR of the Framework app bundle.
    bundle_data("honey_framework_angle_binaries") {
      sources = [
        "$root_out_dir/egl_intermediates/libEGL.dylib",
        "$root_out_dir/egl_intermediates/libGLESv2.dylib",
      ]
      outputs = [
        "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
      ]
      public_deps = [
        "//ui/gl:angle_library_copy",
      ]
    }

    # Add the SwiftShader .dylibs in the MODULE_DIR of the Framework app bundle.
    bundle_data("honey_framework_swiftshader_binaries") {
      sources = [
        "$root_out_dir/vk_intermediates/libvk_swiftshader.dylib",
        "$root_out_dir/vk_intermediates/vk_swiftshader_icd.json",
      ]
      outputs = [
        "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
      ]
      public_deps = [
        "//ui/gl:swiftshader_vk_library_copy",
      ]
    }
  }

  group("honey_framework_angle_library") {
    if (use_egl) {
      deps = [
        ":honey_framework_angle_binaries",
      ]
    }
  }

  group("honey_framework_swiftshader_library") {
    if (use_egl) {
      deps = [
        ":honey_framework_swiftshader_binaries",
      ]
    }
  }

  mac_framework_bundle("honey_framework") {
    # Necessary because the libhoneycomb_test_support target is testonly.
    testonly = true

    output_name = honey_framework_name

    framework_version = "A"
    framework_contents = [
      "Libraries",
      "Resources",
    ]

    sources = includes_common +
              includes_mac +
              gypi_paths.autogen_cpp_includes +
              gypi_paths2.includes_capi +
              gypi_paths.autogen_capi_includes +
              gypi_paths2.libhoneycomb_sources_common +
              gypi_paths.autogen_library_side

    deps = [
      ":honey_framework_angle_library",
      ":honey_framework_resources",
      ":honey_framework_swiftshader_library",
      ":libhoneycomb_static",
      ":libhoneycomb_test_support",
    ]

    configs += [
      ":libhoneycomb_autogen_config",
    ]

    # We don't link the framework so just use the path from the main executable.
    ldflags = [
      "-Wl,-install_name,@executable_path/../Frameworks/$output_name.framework/$output_name",
      "-compatibility_version",
      honey_dylib_version,
      "-current_version",
      honey_dylib_version,
    ]

    if (is_component_build) {
      # Set up the rpath for the framework so that it can find dylibs in the
      # root output directory. The framework is at
      # $app_name.app/Contents/Frameworks/$output_name.framework/Versions/A/$output_name
      # so use loader_path to go back to the root output directory.
      ldflags += [
        "-rpath",
        "@loader_path/../../../../../..",
      ]
    }

    info_plist_target = ":honey_framework_plist"
  }
} else {
  config("pdb_larger_than_4gb") {
    if (is_win && symbol_level == 2) {
      # These binaries create PDBs larger than 4 GiB. Increasing the PDB page
      # size allows larger PDBs, but not all tools can handle such large PDBs
      # yet.
      ldflags = [ "/pdbpagesize:8192" ]
    }
  }

  shared_library("libhoneycomb") {
    # Necessary because the libhoneycomb_test_support target is testonly.
    testonly = true

    sources = includes_common +
              gypi_paths.autogen_cpp_includes +
              gypi_paths2.includes_capi +
              gypi_paths.autogen_capi_includes +
              gypi_paths2.libhoneycomb_sources_common +
              gypi_paths.autogen_library_side

    deps = [
      ":libhoneycomb_static",
      ":libhoneycomb_test_support",
    ]

    configs += [
      ":libhoneycomb_autogen_config",
      ":pdb_larger_than_4gb",
    ]

    if (is_win) {
      sources += includes_win + [
        "libhoneycomb_dll/libhoneycomb_dll.rc",
      ]

      deps += [
        # Bring in ui_unscaled_resources.rc which contains custom cursors.
        # TODO(honey): Remove this once custom cursors can be loaded via
        # ResourceBundle. See crbug.com/147663.
        "//ui/resources:ui_unscaled_resources_grd",
      ]

      # Delay-load as many DLLs as possible for sandbox and startup perf
      # improvements.
      configs += [ "//build/config/win:delayloads" ]
    }

    if (is_linux && !is_debug && !use_partition_alloc_as_malloc) {
      # Only export necessary symbols from libhoneycomb.so.
      # Don't do this in Debug builds because it causes the resulting
      # application to crash.
      # Also need to do this for ASAN builds to work around
      # https://crbug.com/832808.
      ldflags = [ "-Wl,--version-script=" +
                  rebase_path("//honey/libhoneycomb_dll/libhoneycomb.lst") ]
    }
  }
}


#
# Executable/app targets.
#

# Source files for TeamCity GTest integration.
# See tests/gtest/teamcity/README.honey for details.
source_set("gtest_teamcity") {
  testonly = true

  sources = [
    "tests/gtest/teamcity/include/teamcity_gtest.h",
    "tests/gtest/teamcity/src/teamcity_gtest.cpp",
    "tests/gtest/teamcity/src/teamcity_gtest.h",
    "tests/gtest/teamcity/src/teamcity_messages.cpp",
    "tests/gtest/teamcity/src/teamcity_messages.h",
  ]

  deps = [
    "//testing/gtest",
  ]

  configs += [
    "libhoneycomb/features:config",
    "//build/config:precompiled_headers",
  ]
}

if (is_mac) {
  # Helper for generating the Honeycomb app bundle.
  template("honey_app") {
    assert(defined(invoker.helper_info_plist))
    assert(defined(invoker.helper_sources))
    assert(defined(invoker.info_plist))
    assert(defined(invoker.sources))

    app_name = target_name
    app_helper_name = "$app_name Helper"
    app_testonly = defined(invoker.testonly) && invoker.testonly

    tweak_info_plist("${app_name}_helper_plist") {
      testonly = app_testonly
      info_plist = invoker.helper_info_plist
      args = [
        "--breakpad=0",
        "--keystone=0",
        "--scm=0",
        "--version",
        honey_plist_version,
      ]
    }

    template("honey_helper_app") {
      mac_app_bundle(target_name) {
        assert(defined(invoker.helper_sources))
        assert(defined(invoker.helper_name_suffix))
        assert(defined(invoker.helper_bundle_id_suffix))

        testonly = app_testonly
        output_name = app_helper_name + invoker.helper_name_suffix

        sources = invoker.helper_sources

        extra_substitutions = [
          "BUNDLE_ID_SUFFIX=${invoker.helper_bundle_id_suffix}",
        ]

        deps = [
          ":honey_make_headers",
          ":honey_sandbox",
          ":libhoneycomb_dll_wrapper",
        ]
        if (defined(invoker.helper_deps)) {
          deps += invoker.helper_deps
        }

        ldflags = [
          # The helper is in $app_name.app/Contents/Frameworks/$app_name Helper.app/Contents/MacOS/
          # so set rpath up to the base.
          "-rpath",
           "@executable_path/../../../../../..",
        ]

        info_plist_target = ":${app_name}_helper_plist"

        if (defined(invoker.helper_frameworks)) {
          frameworks = invoker.helper_frameworks
        }

        if (defined(invoker.helper_defines)) {
          defines = invoker.helper_defines
        }
      }
    }

    # From //chrome/BUILD.gn:
    # Helper app to display alert notifications. This is necessary as an app can
    # only display either banner or alert style notifications and the main app
    # will display banners.
    alert_helper_params = [
      "alerts",
      ".alerts",
      " (Alerts)",
    ]

    # Merge all helper apps needed by //content and //chrome.
    chrome_mac_helpers = content_mac_helpers + [ alert_helper_params ]

    foreach(helper_params, chrome_mac_helpers) {
      _helper_target = helper_params[0]
      _helper_bundle_id = helper_params[1]
      _helper_suffix = helper_params[2]
      honey_helper_app("${app_name}_helper_app_${_helper_target}") {
        helper_sources = invoker.helper_sources
        if (defined(invoker.helper_deps)) {
          helper_deps = invoker.helper_deps
        }
        if (defined(invoker.helper_frameworks)) {
          helper_frameworks = invoker.helper_frameworks
        }
        if (defined(invoker.helper_defines)) {
          helper_defines = invoker.helper_defines
        }

        helper_name_suffix = _helper_suffix
        helper_bundle_id_suffix = _helper_bundle_id
      }
    }

    bundle_data("${app_name}_framework_bundle_data") {
      testonly = app_testonly
      sources = [
        "$root_out_dir/$honey_framework_name.framework",
      ]

      public_deps = [
        ":honey_framework",
      ]

      foreach(helper_params, chrome_mac_helpers) {
        sources += [
          "$root_out_dir/${app_helper_name}${helper_params[2]}.app",
        ]
        public_deps += [ ":${app_name}_helper_app_${helper_params[0]}" ]
      }

      outputs = [
        "{{bundle_contents_dir}}/Frameworks/{{source_file_part}}",
      ]
    }

    tweak_info_plist("${app_name}_plist") {
      testonly = app_testonly
      info_plist = invoker.info_plist
      args = [
        "--scm=1",
        "--version",
        honey_plist_version,
      ]
    }

    mac_app_bundle(app_name) {
      testonly = app_testonly
      output_name = app_name

      sources = invoker.sources

      public_deps = [ ":libhoneycomb_dll_wrapper" ]
      deps = [
        ":honey_make_headers",
        ":libhoneycomb_dll_wrapper",
        ":${app_name}_framework_bundle_data",
      ]
      if (defined(invoker.deps)) {
        deps += invoker.deps
      }

      if (defined(invoker.frameworks)) {
        frameworks = invoker.frameworks
      }

      if (defined(invoker.defines)) {
        defines = invoker.defines
      }

      info_plist_target = ":${app_name}_plist"
    }
  }


  #
  # honeyclient app targets.
  #

  bundle_data("honeyclient_resources_bundle_data") {
    sources = gypi_paths2.shared_sources_resources +
              gypi_paths2.honeyclient_sources_resources + [
      "tests/honeyclient/resources/mac/honeyclient.icns",
    ]

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }

  bundle_data("honeyclient_resources_bundle_data_extensions_set_page_color") {
    sources = gypi_paths2.honeyclient_sources_resources_extensions_set_page_color
    outputs = [
      "{{bundle_resources_dir}}/extensions/set_page_color/{{source_file_part}}",
    ]
  }

  bundle_data("honeyclient_resources_bundle_data_english") {
    sources = [
      "tests/honeyclient/resources/mac/English.lproj/InfoPlist.strings",
    ]

    outputs = [
      "{{bundle_resources_dir}}/English.lproj/{{source_file_part}}",
    ]
  }

  mac_xib_bundle_data("honeyclient_xibs") {
    sources = [
      "tests/honeyclient/resources/mac/English.lproj/MainMenu.xib",
    ]

    output_path = "{{bundle_resources_dir}}/English.lproj"
  }

  honey_app("honeyclient") {
    # Necessary because the honey_framework target is testonly.
    testonly = true

    helper_info_plist = "tests/honeyclient/resources/mac/helper-Info.plist"
    helper_sources = includes_common +
                     includes_mac +
                     gypi_paths2.includes_wrapper +
                     gypi_paths2.includes_wrapper_mac +
                     gypi_paths2.shared_sources_common +
                     gypi_paths2.shared_sources_renderer +
                     gypi_paths2.shared_sources_mac_helper +
                     gypi_paths2.honeyclient_sources_common +
                     gypi_paths2.honeyclient_sources_renderer
    helper_defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]

    info_plist = "tests/honeyclient/resources/mac/Info.plist"
    sources = includes_common +
              includes_mac +
              gypi_paths2.includes_wrapper +
              gypi_paths2.includes_wrapper_mac +
              gypi_paths2.shared_sources_browser +
              gypi_paths2.shared_sources_common +
              gypi_paths2.shared_sources_mac +
              gypi_paths2.honeyclient_sources_browser +
              gypi_paths2.honeyclient_sources_common +
              gypi_paths2.honeyclient_sources_mac
    deps = [
      ":honeyclient_resources_bundle_data",
      ":honeyclient_resources_bundle_data_extensions_set_page_color",
      ":honeyclient_resources_bundle_data_english",
      ":honeyclient_xibs",
    ]
    frameworks = [
      "AppKit.framework",
      "OpenGL.framework",
    ]
    defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]
  }


  #
  # honeysimple app targets.
  #

  bundle_data("honeysimple_resources_bundle_data") {
    sources = [
      "tests/honeysimple/mac/honeysimple.icns",
    ]

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }

  bundle_data("honeysimple_resources_bundle_data_english") {
    sources = [
      "tests/honeysimple/mac/English.lproj/InfoPlist.strings",
    ]

    outputs = [
      "{{bundle_resources_dir}}/English.lproj/{{source_file_part}}",
    ]
  }

  mac_xib_bundle_data("honeysimple_xibs") {
    sources = [
      "tests/honeysimple/mac/English.lproj/MainMenu.xib",
    ]
    output_path = "{{bundle_resources_dir}}/English.lproj"
  }

  honey_app("honeysimple") {
    # Necessary because the honey_framework target is testonly.
    testonly = true

    helper_info_plist = "tests/honeysimple/mac/helper-Info.plist"
    helper_sources = includes_common +
                     includes_mac +
                     gypi_paths2.includes_wrapper +
                     gypi_paths2.includes_wrapper_mac +
                     gypi_paths2.honeysimple_sources_mac_helper
    helper_defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]

    info_plist = "tests/honeysimple/mac/Info.plist"
    sources = includes_common +
              includes_mac +
              gypi_paths2.includes_wrapper +
              gypi_paths2.includes_wrapper_mac +
              gypi_paths2.honeysimple_sources_common +
              gypi_paths2.honeysimple_sources_mac
    deps = [
      ":honeysimple_resources_bundle_data",
      ":honeysimple_resources_bundle_data_english",
      ":honeysimple_xibs",
    ]
    frameworks = [
      "AppKit.framework",
    ]
    defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]
  }


  #
  # honeytests app targets.
  #

  bundle_data("honeytests_resources_bundle_data") {
    sources = gypi_paths2.shared_sources_resources + [
      "tests/honeytests/resources/mac/honeytests.icns",
    ]

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }

  bundle_data("honeytests_resources_bundle_data_english") {
    sources = [
      "tests/honeytests/resources/mac/English.lproj/InfoPlist.strings",
    ]

    outputs = [
      "{{bundle_resources_dir}}/English.lproj/{{source_file_part}}",
    ]
  }

  mac_xib_bundle_data("honeytests_xibs") {
    sources = [
      "tests/honeytests/resources/mac/English.lproj/MainMenu.xib",
    ]
    output_path = "{{bundle_resources_dir}}/English.lproj"
  }

  honey_app("honeytests") {
    testonly = true

    helper_info_plist = "tests/honeytests/resources/mac/helper-Info.plist"
    helper_sources = gypi_paths2.shared_sources_common +
                     gypi_paths2.shared_sources_renderer +
                     gypi_paths2.shared_sources_mac_helper +
                     gypi_paths2.honeytests_sources_mac_helper
    helper_deps = [
      ":gtest_teamcity",
      "//testing/gtest",
    ]
    helper_frameworks = [
      "AppKit.framework",
    ]
    helper_defines = [
      "HONEYCOMB_USE_SANDBOX",
      "HONEYCOMB_TESTS_IN_SRC_DIRECTORY",
    ]

    info_plist = "tests/honeytests/resources/mac/Info.plist"
    sources = includes_common +
              includes_mac +
              gypi_paths2.includes_wrapper +
              gypi_paths2.includes_wrapper_mac +
              gypi_paths2.shared_sources_browser +
              gypi_paths2.shared_sources_common +
              gypi_paths2.shared_sources_mac +
              gypi_paths2.honeytests_sources_common +
              gypi_paths2.honeytests_sources_mac
    deps = [
      ":honeytests_resources_bundle_data",
      ":honeytests_resources_bundle_data_english",
      ":honeytests_xibs",
      ":gtest_teamcity",
      "//testing/gtest",
    ]
    frameworks = [
      "AppKit.framework",
    ]
    defines = [
      "HONEYCOMB_USE_SANDBOX",
      "HONEYCOMB_TESTS_IN_SRC_DIRECTORY",
    ]
  }
} else {
  #
  # honeyclient targets.
  #

  # The honeyclient target depends on packages that are not available in the
  # default sysroot environment.
  if (is_linux && !use_sysroot) {
    pkg_config("glib") {
      packages = [
        "glib-2.0",
      ]
    }
  }

  if (is_linux && honey_use_gtk) {
    pkg_config("gtk") {
      packages = [
        "gmodule-2.0",
        "gtk+-3.0",
        "gthread-2.0",
        "gtk+-unix-print-3.0",
        "xi",
      ]
    }
  }

  if (is_linux) {
    copy("copy_honeyclient_files") {
      sources = gypi_paths2.shared_sources_resources +
                gypi_paths2.honeyclient_sources_resources
      outputs = [ "${root_out_dir}/honeyclient_files/{{source_file_part}}" ]
    }

    copy("copy_honeyclient_files_extensions_set_page_color") {
      sources = gypi_paths2.honeyclient_sources_resources_extensions_set_page_color
      outputs = [ "${root_out_dir}/honeyclient_files/extensions/set_page_color/{{source_file_part}}" ]
    }
  }

  executable("honeyclient") {
    # Necessary because the libhoneycomb target is testonly.
    testonly = true

    sources = includes_common +
              gypi_paths2.includes_wrapper +
              gypi_paths2.shared_sources_browser +
              gypi_paths2.shared_sources_common +
              gypi_paths2.shared_sources_renderer +
              gypi_paths2.honeyclient_sources_browser +
              gypi_paths2.honeyclient_sources_common +
              gypi_paths2.honeyclient_sources_renderer

    deps = [
      ":libhoneycomb",
      ":libhoneycomb_dll_wrapper",
    ]

    defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]

    if (is_win) {
      sources += includes_win +
                 gypi_paths2.shared_sources_win +
                 gypi_paths2.honeyclient_sources_win

      # Set /SUBSYSTEM:WINDOWS.
      configs -= [ "//build/config/win:console" ]
      configs += [ "//build/config/win:windowed" ]

      # Delay-load as many DLLs as possible for sandbox and startup perf
      # improvements.
      configs += [ "//build/config/win:delayloads" ]

      defines += [
        "HONEYCOMB_USE_ATL",
      ]

      deps += [
        ":honey_sandbox",
        "//build/win:default_exe_manifest",
      ]

      libs = [
        "comctl32.lib",
        "d3d11.lib",
        "imm32.lib",
        "oleacc.lib",
        "rpcrt4.lib",
        "shlwapi.lib",
      ]

      if (target_cpu != "arm64") {
        libs += [
          "opengl32.lib",
          "glu32.lib"
        ]
      }
    }

    if (is_linux) {
      sources += includes_linux +
                 gypi_paths2.shared_sources_linux +
                 gypi_paths2.honeyclient_sources_linux

      deps += [
        ":copy_honeyclient_files",
        ":copy_honeyclient_files_extensions_set_page_color",
      ]

      libs = [
        "GL",
        "X11",
      ]

      if (honey_use_gtk) {
        configs += [
          ":gtk",
        ]
        cflags = [
          # Don't warn about deprecated GDK/GTK functions.
          "-Wno-deprecated-declarations",
        ]
      }

      if (!is_component_build) {
        # Set rpath to find our own libfreetype even in a non-component build.
        configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
      }
    }
  }


  #
  # honeysimple targets.
  #

  executable("honeysimple") {
    # Necessary because the libhoneycomb target is testonly.
    testonly = true

    sources = includes_common +
              gypi_paths2.includes_wrapper +
              gypi_paths2.honeysimple_sources_common

    deps = [
      ":libhoneycomb",
      ":libhoneycomb_dll_wrapper",
    ]

    defines = [
      "HONEYCOMB_USE_SANDBOX",
    ]

    if (is_win) {
      sources += includes_win +
                 gypi_paths2.honeysimple_sources_win

      # Set /SUBSYSTEM:WINDOWS.
      configs -= [ "//build/config/win:console" ]
      configs += [ "//build/config/win:windowed" ]

      # Delay-load as many DLLs as possible for sandbox and startup perf
      # improvements.
      configs += [ "//build/config/win:delayloads" ]

      deps += [
        ":honey_sandbox",
        "//build/win:default_exe_manifest",
      ]

      libs = [
        "comctl32.lib",
        "shlwapi.lib",
        "rpcrt4.lib",
      ]
    }

    if (is_linux) {
      sources += includes_linux +
                 gypi_paths2.honeysimple_sources_linux

      if (ozone_platform_x11) {
        libs = [
          "X11",
        ]
      }

      if (!is_component_build) {
        # Set rpath to find our own libfreetype even in a non-component build.
        configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
      }
    }
  }


  #
  # honeytests targets.
  #

  if (is_linux) {
    copy("copy_honeytests_files") {
      sources = gypi_paths2.shared_sources_resources
      outputs = [ "${root_out_dir}/honeytests_files/{{source_file_part}}" ]
    }
  }

  executable("honeytests") {
    testonly = true

    sources = includes_common +
              gypi_paths2.includes_wrapper +
              gypi_paths2.shared_sources_browser +
              gypi_paths2.shared_sources_common +
              gypi_paths2.shared_sources_renderer +
              gypi_paths2.honeytests_sources_common

    deps = [
      ":libhoneycomb",
      ":libhoneycomb_dll_wrapper",
      ":gtest_teamcity",
      "//testing/gtest",
    ]

    defines = [
      "HONEYCOMB_USE_SANDBOX",
      "HONEYCOMB_TESTS_IN_SRC_DIRECTORY",
    ]

    if (is_win) {
      sources += gypi_paths2.shared_sources_win +
                 gypi_paths2.honeytests_sources_win

      # Delay-load as many DLLs as possible for sandbox and startup perf
      # improvements.
      configs += [ "//build/config/win:delayloads" ]

      deps += [
        ":honey_sandbox",
        "//build/win:default_exe_manifest",
      ]
    }

    if (is_linux) {
      sources += gypi_paths2.shared_sources_linux +
                 gypi_paths2.honeytests_sources_linux

      if (ozone_platform_x11) {
        libs = [
          "X11",
        ]
      } else {
        if (!use_sysroot) {
          configs += [ ":glib" ]
        }
      }

      deps += [
        ":copy_honeytests_files",
      ]
    }

    if (is_linux && !is_component_build) {
      # Set rpath to find our own libfreetype even in a non-component build.
      configs += [ "//build/config/gcc:rpath_for_built_shared_libraries" ]
    }
  }
}
